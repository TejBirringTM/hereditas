
FILE_GRAMMAR ::= STATEMENT_LINE*

STATEMENT ::= PROGENY_DECLARATION | MARRIAGE_DECLARATION | ADOPTED_PROGENY_DECLARATION | MALE_DECLARATION | FEMALE_DECLARATION | COMMENT
STATEMENT_LINE ::= WHITESPACE* STATEMENT WHITESPACE*

CR ::= #x0D /* carriage return */
LF ::= #x0A /* line feed */
NEWLINE ::= CR LF | LF CR | CR | LF
SPACE ::= #x20
TAB ::= #x09
SPACE_TAB ::= SPACE | TAB
WHITESPACE ::= SPACE_TAB | NEWLINE

NON_ZERO_DIGIT ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
DIGIT ::= "0" | NON_ZERO_DIGIT
HEX ::= DIGIT | "A" | "B" | "C" | "D" | "E" | "F"
NATURAL_NUMBER ::= NON_ZERO_DIGIT DIGIT* 

ALPHABET_LOWERCASE ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "ā" | "ē" | "ī" | "ō" | "ū" | "ṅ" | "ṭ" | "ḍ" | "ṛ"
ALPHABET_UPPERCASE ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
ALPHABET ::= ALPHABET_LOWERCASE | ALPHABET_UPPERCASE

SPECIAL_CHAR ::= "#" | "@" | "$" | "%" | "^" | "&" | "*" | "(" | ")" | "-" | "_" | "+" | "=" | "{" | "}" | "[" | "]" | ":" | ";" | "'" | "<" | ">" | "," | "." | "?" | "/" | "`" | "~"

ALPHANUMERIC ::= DIGIT | ALPHABET
CHAR ::= ALPHANUMERIC | SPECIAL_CHAR | SPACE_TAB

STRING ::= '"' CHAR* '"'

DECLARATION_KEY ::= (ALPHABET | NON_ZERO_DIGIT) (ALPHABET | "." | DIGIT)*

MALE_DECLARATION ::= "[" DECLARATION_KEY ":" SPACE_TAB* STRING SPACE_TAB* "]"
MALE_REFERENCE ::= "[" DECLARATION_KEY "]"
MALE_EXPRESSION ::= MALE_DECLARATION | MALE_REFERENCE

FEMALE_DECLARATION ::= "(" DECLARATION_KEY ":" SPACE_TAB* STRING SPACE_TAB* ")"
FEMALE_REFERENCE ::= "(" DECLARATION_KEY ")"
FEMALE_EXPRESSION ::= FEMALE_DECLARATION | FEMALE_REFERENCE

PERSON_EXPRESSION ::= MALE_DECLARATION | MALE_REFERENCE | FEMALE_DECLARATION | FEMALE_REFERENCE

FWD_RELATIONSHIP_ARROW_BEGIN ::= SPACE_TAB* "-"+ "{" SPACE_TAB*
FWD_RELATIONSHIP_ARROW_END ::=  SPACE_TAB* "}" "-"+ ">" SPACE_TAB*

MUTUAL_RELATIONSHIP_ARROW_BEGIN ::= SPACE_TAB* "-"+ "{" SPACE_TAB*
MUTUAL_RELATIONSHIP_ARROW_END ::=  SPACE_TAB* "}" "-"+ SPACE_TAB*

ADOPTED_PROGENY_DECLARATION_KEY ::= "adp:" DECLARATION_KEY
ADOPTED_PROGENY_DECLARATION ::= (PERSON_EXPRESSION | MARRIAGE_REFERENCE) FWD_RELATIONSHIP_ARROW_BEGIN (ADOPTED_PROGENY_DECLARATION_KEY | "adp") FWD_RELATIONSHIP_ARROW_END PERSON_EXPRESSION
ADOPTED_PROGENY_REFERENCE ::= "{" ADOPTED_PROGENY_DECLARATION_KEY "}"

MARRIAGE_DECLARATION_KEY ::= "mrg:" DECLARATION_KEY
MARRIAGE_DECLARATION ::= PERSON_EXPRESSION MUTUAL_RELATIONSHIP_ARROW_BEGIN (MARRIAGE_DECLARATION_KEY | "mrg") MUTUAL_RELATIONSHIP_ARROW_END PERSON_EXPRESSION
MARRIAGE_REFERENCE ::= "{" MARRIAGE_DECLARATION_KEY "}"

PROGENY_DECLARATION_KEY ::= "dsc:" DECLARATION_KEY
PROGENY_DECLARATION ::= MARRIAGE_REFERENCE SPACE_TAB* FWD_RELATIONSHIP_ARROW_BEGIN (PROGENY_DECLARATION_KEY | "dsc") FWD_RELATIONSHIP_ARROW_END PERSON_EXPRESSION
PROGENY_REFERENCE ::= "{" PROGENY_DECLARATION_KEY "}"

COMMENT ::= "/*" (ALPHANUMERIC | SPACE_TAB)* "*/"

FILE_GRAMMAR ::= STATEMENT_LINE*

STATEMENT ::= APPEND_TEXT_DECLARATION | PROGENY_DECLARATION | MARRIAGE_DECLARATION | ADOPTED_PROGENY_DECLARATION | MALE_DECLARATION | FEMALE_DECLARATION | COMMENT | START_DECLARATION
STATEMENT_LINE ::= WHITESPACE* STATEMENT WHITESPACE*

CR ::= #x0D /* carriage return */
LF ::= #x0A /* line feed */
NEWLINE ::= CR LF | LF CR | CR | LF
SPACE ::= #x20
TAB ::= #x09
SPACE_TAB ::= SPACE | TAB
WHITESPACE ::= SPACE_TAB | NEWLINE

NON_ZERO_DIGIT ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
DIGIT ::= "0" | NON_ZERO_DIGIT
HEX ::= DIGIT | "A" | "B" | "C" | "D" | "E" | "F"
NATURAL_NUMBER ::= NON_ZERO_DIGIT DIGIT* 

ALPHABET_LOWERCASE ::= [#x0061-#x007A] | [#x00DF-#x00F6] | [#x00F8-#x00FF] | [#x0101-#x0137] | [#x0138-#x0148] | [#x0149-#x017E] | [#x017F] | [#x0180-#x01BA] | [#x01BB] | [#x01BC-#x01BF] | [#x01C0-#x01C3] | [#x01C4-#x01CD] | [#x1E00-#x1E95] | [#x1E96-#x1E9F] | [#x1EA0-#x1EF9]

ALPHABET_UPPERCASE ::= [#x0041-#x005A] | [#x00C0-#x00D6] | [#x00D8-#x00DE] | [#x0100-#x0136] | [#x0139-#x0147] | [#x014A-#x017D] | [#x0181-#x0182] | [#x0184-#x0186] | [#x0187-#x0189] | [#x018A-#x018F] | [#x1E00-#x1E94] | [#x1EA0-#x1EF8]

DIACRITICAL_MARKS ::= [#x0300-#x036F] | [#x1AB0-#x1AFF] | [#x1DC0-#x1DFF] | [#x20D0-#x20FF] | [#xFE20-#xFE2F] | [#x1CD0-#x1CFA] | [#x0900-#x0903] | [#x093A-#x094F] | [#x0980-#x0983] | [#x09BE-#x09CC] | [#x0E31] | [#x0E34-#x0E3A] | [#x0E47-#x0E4E] | [#x1031-#x1037] | [#x103A-#x103E] | [#x0F18-#x0F19] | [#x0F35] | [#x0F37] | [#x0F39] | [#x0F71-#x0F7E] | [#x0F80-#x0F84] | [#x064B-#x0655] | [#x0670]

ALPHABET ::= (ALPHABET_LOWERCASE | ALPHABET_UPPERCASE) DIACRITICAL_MARKS?

BASE_CHAR ::= "#" | "@" | "$" | "%" | "^" | "&" | "(" | ")" | "-" | "_" | "+" | "=" | "{" | "}" | "[" | "]" | ":" | ";" | "'" | "<" | ">" | "," | "." | "?" | "\" | "`" | "~"
TEXT_CHAR ::= BASE_CHAR | "*" | "/" | '"'
STRING_CHAR ::= BASE_CHAR | "*" | "/"
COMMENT_CHAR ::= BASE_CHAR | '"'

ALPHANUMERIC ::= DIGIT | ALPHABET
CHAR ::= ALPHANUMERIC | STRING_CHAR | SPACE_TAB

STRING ::= '"' CHAR* '"'
TEXTCHAR ::= ALPHANUMERIC | TEXT_CHAR | SPACE_TAB
COMMENTCHAR ::= ALPHANUMERIC | COMMENT_CHAR | SPACE_TAB
TEXT ::= TEXTCHAR+

DECLARATION_KEY ::= (ALPHABET | NON_ZERO_DIGIT) (ALPHABET | "." | "_" | "-" | DIGIT)*

REDACTED_ATTRIBUTE ::= "REDACTED"

PERSON_DECLARATION_ATTRIBUTE ::= REDACTED_ATTRIBUTE
PERSON_DECLARATION_ATTRIBUTES ::= (PERSON_DECLARATION_ATTRIBUTE SPACE_TAB*)

MALE_DECLARATION ::= "[" DECLARATION_KEY ":" SPACE_TAB* STRING SPACE_TAB* PERSON_DECLARATION_ATTRIBUTES? "]"
MALE_REFERENCE ::= "[" DECLARATION_KEY "]"
MALE_EXPRESSION ::= MALE_DECLARATION | MALE_REFERENCE

FEMALE_DECLARATION ::= "(" DECLARATION_KEY ":" SPACE_TAB* STRING SPACE_TAB* PERSON_DECLARATION_ATTRIBUTES? ")"
FEMALE_REFERENCE ::= "(" DECLARATION_KEY ")"
FEMALE_EXPRESSION ::= FEMALE_DECLARATION | FEMALE_REFERENCE

PERSON_EXPRESSION ::= MALE_DECLARATION | MALE_REFERENCE | FEMALE_DECLARATION | FEMALE_REFERENCE

FWD_RELATIONSHIP_ARROW_BEGIN ::= SPACE_TAB* "-"+ "{" SPACE_TAB*
FWD_RELATIONSHIP_ARROW_END ::=  SPACE_TAB* "}" "-"+ ">" SPACE_TAB*

MUTUAL_RELATIONSHIP_ARROW_BEGIN ::= SPACE_TAB* "-"+ "{" SPACE_TAB*
MUTUAL_RELATIONSHIP_ARROW_END ::=  SPACE_TAB* "}" "-"+ SPACE_TAB*

ADOPTED_PROGENY_DECLARATION_KEY ::= "adp:" DECLARATION_KEY
ADOPTED_PROGENY_DECLARATION ::= (PERSON_EXPRESSION | MARRIAGE_REFERENCE) FWD_RELATIONSHIP_ARROW_BEGIN (ADOPTED_PROGENY_DECLARATION_KEY | "adp") FWD_RELATIONSHIP_ARROW_END PERSON_EXPRESSION
ADOPTED_PROGENY_REFERENCE ::= "{" ADOPTED_PROGENY_DECLARATION_KEY "}"

MARRIAGE_DECLARATION_KEY ::= "mrg:" DECLARATION_KEY
MARRIAGE_DECLARATION ::= PERSON_EXPRESSION MUTUAL_RELATIONSHIP_ARROW_BEGIN (MARRIAGE_DECLARATION_KEY | "mrg") MUTUAL_RELATIONSHIP_ARROW_END PERSON_EXPRESSION
MARRIAGE_REFERENCE ::= "{" MARRIAGE_DECLARATION_KEY "}"

PROGENY_DECLARATION_KEY ::= "dsc:" DECLARATION_KEY
PROGENY_DECLARATION ::= MARRIAGE_REFERENCE SPACE_TAB* FWD_RELATIONSHIP_ARROW_BEGIN (PROGENY_DECLARATION_KEY | "dsc") FWD_RELATIONSHIP_ARROW_END PERSON_EXPRESSION
PROGENY_REFERENCE ::= "{" PROGENY_DECLARATION_KEY "}"

START_DECLARATION ::= "START WITH" SPACE_TAB+ MALE_EXPRESSION

COMMENT ::= "/*" COMMENTCHAR* "*/"

APPEND_TEXT_DECLARATION_LINE ::= TEXT NEWLINE
APPEND_TEXT_DECLARATION ::= "APPEND TEXT" SPACE_TAB+ (PERSON_EXPRESSION | MARRIAGE_REFERENCE) SPACE_TAB* ":" (SPACE_TAB | NEWLINE)* APPEND_TEXT_DECLARATION_LINE+
